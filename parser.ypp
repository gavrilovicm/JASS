%{
#include<iostream>
#include<cstdlib>
#include<string>
#include<cstring>
#include<cstdio>
#include"userinfo.hpp"

extern int yylex();

void yyerror(std::string s){
	std::cerr << "Error occurred: " << s << std::endl;
}

std::string* username;
%}

%union{
	int int_type;
	char* s;
}

%left '+' '-'
%left '*' '/' '%'
%left UNMIN

%type<int_type> E
%token<int_type> num_tok
%token<s> ls_flags_tok id_tok
%token clear_tok ls_tok pwd_tok chusername_tok

%%

Program
	: Niz_linija
	;

Niz_linija
	: Niz_linija wLinija
	| wLinija
	;

wLinija
	: Linija '\n'{
		std::cout << *username << ":->";
	}
	| '\n'{
		std::cout << *username << ":->";
	}
	| error '\n' {
		/* TODO - show more userfriendly message */
		std::cout << "It has come to an error. Please rephrase.." << std::endl;
		std::cout << *username << ":->";
	}
	;

Linija
	: E {std::cout << $1 << std::endl;}
	| clear_tok {clear();}
	| ls_tok ls_flags_tok {
		/* TODO - implement with system functions without system()*/
		if (strlen($2) > 10){
			std::cout   << "Too many flags sent.."<< std::endl;
		}else{
			char buffer[11];
			snprintf(buffer, sizeof(buffer), "ls %s", $2);
			system(buffer);
		}
	}
	| ls_tok {system("ls");}
	| pwd_tok {system("pwd");}
	| chusername_tok id_tok {
		username = new std::string($2);
	}
	;

E
	: E '+' E {$$ = $1 + $3;}
	| E '-' E {$$ = $1 - $3;}
	| E '*' E {$$ = $1 * $3;}
	| E '/' E {$$ = $1 / $3;}
	| E '%' E {$$ = $1 % $3;}
	| '(' E ')' {
		$$ = $2;
	}
	| '-' E %prec UNMIN {$$ = -$2;}
	| num_tok {
		$$ = $1;
	}
	;
%%

int main(){

	clear();

	username = getuserinfo();

	std::cout << *username << ":->";


	yyparse();
	delete username;
	exit(EXIT_SUCCESS);
}
