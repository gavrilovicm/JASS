%{
#include<iostream>
#include<cstdlib>
#include<string>
#include<cstring>
#include<cstdio>
#include"userinfo.hpp"

#define show_powerline {std::cout << *username << "::" << *wdir << ":->";}

extern int yylex();

void yyerror(std::string s){
	std::cerr << "Error occurred: " << s << std::endl;
}

std::string* username;
std::string* wdir;
bool matlab_mode = false;
%}

%union{
	int int_type;
	char* s;
}

%left '+' '-'
%left '*' '/' '%'
%left UNMIN

%type<int_type> E
%token<int_type> num_tok
%token<s> ls_flags_tok id_tok
%token clear_tok ls_tok pwd_tok chusername_tok exit_tok matlab_tok

%%

Program
	: Niz_linija
	;

Niz_linija
	: Niz_linija wLinija
	| wLinija
	;

wLinija
	: Linija '\n'{
		show_powerline
	}
	| '\n'{
		show_powerline
	}
	| error '\n' {
		/* TODO - show more userfriendly message */
		std::cout << "It has come to an error. Please rephrase.." << std::endl;
		show_powerline
	}
	| exit_tok {
		if (matlab_mode){
			std::cout << "Exit matlab mode."
			<< std::endl
			<< std::endl;
			wdir = getworkingdir(username);
			matlab_mode = false;
		}else{
			std::cerr << "exit" << std::endl;
			exit(EXIT_SUCCESS);
		}
	}
	| matlab_tok '\n' {
		std::cout << "Matlab mode active. Please see quick start in README.md for matlab."
		<< std::endl
		<< std::endl;
		wdir = new std::string("matlab-mode");
		matlab_mode = true;
		show_powerline
	}
	;

Linija
	: E {std::cout << $1 << std::endl;}
	| clear_tok {clear();}
	| ls_tok ls_flags_tok {
		/* TODO - implement with system functions without system()*/
		if (strlen($2) > 10){
			std::cout   << "Too many flags sent.."<< std::endl;
		}else{
			char buffer[11];
			snprintf(buffer, sizeof(buffer), "ls %s", $2);
			system(buffer);
		}
	}
	| ls_tok {system("ls");}
	| pwd_tok {system("pwd");}
	| chusername_tok id_tok {
		delete username;
		username = new std::string($2);
	}
	;

E
	: E '+' E {$$ = $1 + $3;}
	| E '-' E {$$ = $1 - $3;}
	| E '*' E {$$ = $1 * $3;}
	| E '/' E {$$ = $1 / $3;}
	| E '%' E {$$ = $1 % $3;}
	| '(' E ')' {
		$$ = $2;
	}
	| '-' E %prec UNMIN {$$ = -$2;}
	| num_tok {
		$$ = $1;
	}
	;
%%

int main(){

	clear();

	username = getuserinfo();

	wdir = getworkingdir(username);

	show_powerline

	yyparse();
	delete username;
	exit(EXIT_SUCCESS);
}
